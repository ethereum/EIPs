---
eip: 7092
title: Financial Bonds
description: Represents debt issued by entities to investors
author: Samuel Gwlanold Edoumou (@Edoumou)
discussions-to: https://ethereum-magicians.org/t/financial-bonds/14461
status: Draft
type: Standards Track
category: ERC
created: 2023-05-28
---

## Abstract

This proposal introduces fixed-income financial bonds with key characteristics defined to facilitate bond issuance in the primary market and enable buying or selling bonds in the secondary market.

## Motivation

Fixed-income instruments are a widely utilized asset class for corporations and other entities raising funds. However, transitioning to tokenized bonds is challenging due to existing standards like [ERC-3475](./eip-3475.md), which introduces unfamiliar concepts and leads to unnecessary gas consumption. Additionally, the lack of named variables like coupon, maturity date, and principal, makes it difficult to implement ERC-3475 since developers need to remember which metadata is assigned to each parameter.

## Specification

The key words "MUST", "MUST NOT", "REQUIRED", "SHALL", "SHALL NOT", "SHOULD", "SHOULD NOT", "RECOMMENDED", "NOT RECOMMENDED", "MAY", and "OPTIONAL" in this document are to be interpreted as described in RFC 2119 and RFC 8174.

**Every contract compliant with this ERC MUST implement the following interface:**

```solidity
pragma solidity ^0.8.0;

/**
 * @title ERC-7092 Financial Bonds Standard
 */
interface IERC7092 {
    /**
     * @notice Returns the bond ISIN
     */
    function isin() external view returns (string memory);

    /**
     * @notice Returns the bond name
     */
    function name() external view returns (string memory);

    /**
     * @notice Returns the bond symbol
     *         It is RECOMMENDED to represent the symbol as a combination of the issuer's short name and the maturity date.
     *         Ex: If a company named Green Energy issues bonds that will mature on October 25, 2030, the bond symbol could be `GE30` or `GE2030` or `GE102530`.
     *
     * OPTIONAL - interfaces and other contracts MUST NOT expect these values to be present. The method is used to improve usability.
     */
    function symbol() external view returns (string memory);

    /**
     * @notice Returns the number of decimals the bond uses - e.g., `10` means to divide the token amount by `10000000000`.
     *
     * OPTIONAL - interfaces and other contracts MUST NOT expect these values to be present. The method is used to improve usability.
     */
    function decimals() external view returns (uint8);

    /**
     * @notice Returns the bond currency. This is the contract address of the token used to pay and return the bond principal.
     */
    function currency() external view returns (address);

    /**
     * @notice Returns the coupon currency. This is the contract address of the token used to pay coupons. It can be the same as the one used for the principal.
     *
     * OPTIONAL - interfaces and other contracts MUST NOT expect these values to be present. The method is used to improve usability.
     */
    function currencyOfCoupon() external view returns (address);

    /**
     * @notice Returns the bond denomination. This is the minimum amount in which the Bonds may be issued. It must be expressed in unit of the principal currency.
     *         Ex: If the denomination is equal to 1,000 and the currency is USDC, then bond denomination is equal to 1,000 USDC.
     */
    function denomination() external view returns (uint256);

    /**
     * @notice Returns the issue volume (total debt amount). It is RECOMMENDED to express the issue volume in the bond currency unit (USDC, DAI, etc.).
     *         NOTICE: The `issue volume` can also be expressed in `denomination` unit. In that case, the `issue volume` MUST equal the `totalSupply`
     *                 of the bonds, i.e., the total number of bond tokens issued.
     *         Ex: if denomination = $1,000, and the total debt is $5,000,000,
     *         then, issueVolume() = $5,000,000 / $1,000 = 5,000 bonds.
     */
    function issueVolume() external view returns (uint256);

    /**
     * @notice Returns the bond interest rate. It is RECOMMENDED to express the interest rate in basis point unit.
     *         1 basis point = 0.01% = 0.0001
     *         Ex: if interest rate = 5%, then coupon() => 500 basis points.
     */
    function couponRate() external view returns (uint256);

    /**
     * @notice Returns the coupon type
     *         Ex: 0: Zero coupon, 1: Fixed Rate, 2: Floating Rate, etc...
     *
     * OPTIONAL - interfaces and other contracts MUST NOT expect these values to be present. The method is used to improve usability.
     */
    function couponType() external view returns (uint256);

    /**
     * @notice Returns the coupon frequency, i.e., the number of times coupons are paid in a year.
     *
     * OPTIONAL - interfaces and other contracts MUST NOT expect these values to be present. The method is used to improve usability.
     */
    function couponFrequency() external view returns (uint256);

    /**
     * @notice Returns the date when bonds were issued to investors. This is a Unix Timestamp like the one returned by block.timestamp.
     */
    function issueDate() external view returns (uint256);

    /**
     * @notice Returns the bond maturity date, i.e., the date when the principal is repaid. This is a Unix Timestamp like the one returned by block.timestamp.
     *         The `maturity date` MUST be greater than the `issue date`.
     */
    function maturityDate() external view returns (uint256);

    /**
     * @notice Returns the day count basis
     *         Ex: 0: actual/actual, 1: actual/360, etc...
     *
     * OPTIONAL - interfaces and other contracts MUST NOT expect these values to be present. The method is used to improve usability.
     */
    function dayCountBasis() external view returns (uint256);

    /**
     * @notice Returns the principal of an account. It is RECOMMENDED to express the principal in the bond currency unit (USDC, DAI, etc.).
     *         NOTICE: The `principal` can also be expressed in `denomination` unit. In that case, the `principal` MUST equal the balance of `_account`.
     *         Ex: if denomination = $1,000, and the user has invested $5,000 (principal in currency unit), then
     *             principalOf(_account) = 5,000 / 1,000 = 5.
     * @param _account account address
     */
    function principalOf(address _account) external view returns (uint256);

    /**
     * @notice Returns the amount of tokens the `_spender` account has been authorized by the `_owner`
     *         account to manage their bonds.
     * @param _owner the bondholder address
     * @param _spender the address that has been authorized by the bondholder
     */
    function approval(address _owner, address _spender) external view returns (uint256);

    /**
     * @notice Authorizes `_spender` account to manage `_amount` of their bonds.
     * @param _spender the address to be authorized by the bondholder
     * @param _amount amount of bond to approve. _amount MUST be a multiple of denomination.
     */
    function approve(address _spender, uint256 _amount) external returns (bool);

    /**
     * @notice Authorizes the `_spender` account to manage all their bonds.
     * @param _spender the address to be authorized by the bondholder
     *
     * OPTIONAL - interfaces and other contracts MUST NOT expect these values to be present. The method is used to improve usability.
     */
    function approveAll(address _spender) external returns (bool);

    /**
     * @notice Lowers the allowance of `_spender` by `_amount`.
     * @param _spender The address to be authorized by the bondholder.
     * @param _amount The amount of bond to remove approval; _amount MUST be a multiple of denomination.
     */
    function decreaseAllowance(address _spender, uint256 _amount) external returns (bool);

    /**
     * @notice Removes the allowance for `_spender`.
     * @param _spender The address to remove the authorization from.
     *
     * OPTIONAL - interfaces and other contracts MUST NOT expect these values to be present. The method is used to improve usability.
     */
    function decreaseAllowanceForAll(address _spender) external returns (bool);

    /**
     * @notice Moves `_amount` bonds to address `_to`.
     * @param _to The address to send the bonds to.
     * @param _amount The amount of bond to transfer. _amount MUST be a multiple of denomination.
     * @param _data Additional information provided by the token holder.
     */
    function transfer(address _to, uint256 _amount, bytes calldata _data) external returns (bool);

    /**
     * @notice Moves all bonds to address `_to`.
     * @param _to The address to send the bonds to.
     * @param _data Additional information provided by the token holder.
     *
     * OPTIONAL - interfaces and other contracts MUST NOT expect these values to be present. The method is used to improve usability.
     */
    function transferAll(address _to, bytes calldata _data) external returns (bool);

    /**
     * @notice Moves `_amount` bonds from an account that has authorized through the approve function.
     * @param _from The bondholder address.
     * @param _to The address to transfer bonds to.
     * @param _amount The amount of bond to transfer. _amount MUST be a multiple of denomination.
     * @param _data Additional information provided by the token holder.
     */
    function transferFrom(address _from, address _to, uint256 _amount, bytes calldata _data) external returns (bool);

    /**
     * @notice Moves all bonds from `_from` to `_to`. The caller must have been authorized through the approve function.
     * @param _from The bondholder address.
     * @param _to The address to transfer bonds to.
     * @param _data Additional information provided by the token holder.
     *
     * OPTIONAL - interfaces and other contracts MUST NOT expect these values to be present. The method is used to improve usability.
     */
    function transferAllFrom(address _from, address _to, bytes calldata _data) external returns (bool);

    /**
     * @notice MUST be emitted when bonds are transferred.
     * @param _from The account that owns bonds.
     * @param _to The account that receives the bond.
     * @param _amount The amount of bonds to be transferred.
     * @param _data Additional information provided by the token holder.
     */
    event Transferred(address indexed _from, address indexed _to, uint256 _amount, bytes _data);

    /**
     * @notice MUST be emitted when an account is approved.
     * @param _owner The bonds owner.
     * @param _spender The account to be allowed to spend bonds.
     * @param _amount The amount allowed by _owner to be spent by _spender.
     */
    event Approved(address indexed _owner, address indexed _spender, uint256 _amount);

    /**
     * @notice MUST be emitted when the `_owner` decreases allowance from `_spender` by quantity `_amount`.
     * @param _owner The bonds owner.
     * @param _spender The account that has been allowed to spend bonds.
     * @param _amount The amount of tokens to disapprove.
     */
    event AllowanceDecreased(address indexed _owner, address indexed _spender, uint256 _amount);
}

```

## Rationale

The design of this ERC aims to simplify the migration to tokenized bonds by maintaining consistency with traditional bond standards. This approach allows fixed-income instruments to be represented as on-chain tokens, manageable through wallets, and utilized by applications like decentralized exchanges, while avoiding the complexities and inefficiencies associated with other standards. This ERC facilitates the creation of new bond tokens with characteristics akin to traditional bonds, enhancing accessibility, liquidity, and cost-efficiency in bond trading and management.

The use of traditional finance terminology, like `issueVolume` and `principalOf`, is aimed at maintaining consistency with traditional bond language, which eases the adaptation for traditional entities.

### Total Supply and Account Balance

The `totalSupply` and `balanceOf` functions are not defined as they can be derived from `issueVolume` and `principalOf`, and `denomination`. However, it's RECOMMENDED to define these functions in any contract implementing this standard, ensuring the proper relationship between these values.

```javascript
    totalSupply = issueVolume / denomination
    balanceOf(account) = principalOf(account) / denomination
```

## Backwards Compatibility

This ERC SHOULD NOT extend existing standards like [ERC-20](./eip-20.md) or [ERC-1155](./eip-1155.md) due to the absence of certain functions like `totalSupply` or `balanceOf`. A pure implementation of this standard is RECOMMENDED for issuing tokenized bonds, as any hybrid solution with other mentioned standards SHOULD fail.

## Reference Implementation

The complete Reference Implementation can be found [here](../assets/eip-7092/ERC7092.sol).

Some bonds have embedded options attached to them. Here are a few examples:

1. Callable bonds that have an option that gives the issuer the right to retire bonds before they mature.
2. Puttable bonds that have an option that gives investors the right to retire bonds before they mature.
3. Convertible bonds that gives investors the right to convert their bonds to equity.

Bonds with embedded options can be created by inheriting from the correct interface.

### Callable Bonds

```solidity
pragma solidity ^0.8.0;
import 'ERC7092.sol';
contract ERC7092Callable is ERC7092 {
    // WRITE THE LOGIC TO ALLOW THE ISSUER TO CALL BONDS
    // STATE VARIABLES AND FUNCTIONS NEEDED
    
    /**
    * @notice call bonds owned by `_investor`
    *         MUST be called by the issuer only
    */
    function call(address _investor) public {
        require(_principals[_investor] > 0, "ERC7092Callable: ONLY_ISSUER");
        require(block.timestamp < _bond[bondISIN].maturityDate, "ERC7092Callable: BOND_MATURED");
        
        uint256 principal =  _principals[_investor];
        _principals[_investor] = 0;
        
        // ADD LOGIC HERE
    }
}
```

### Puttable Bonds

```solidity
pragma solidity ^0.8.0;
import 'ERC7092.sol';
contract ERC7092Puttable is ERC7092 {
    // WRITE THE LOGIC TO ALLOW INVESTORS TO PUT BONDS
    // STATE VARIABLES AND FUNCTIONS NEEDED
    
    /**
    * @notice put bonds
    *         MUST be called by investors who own bonds
    */
    function put() public {
        require(_principals[msg.sender] > 0, "ERC7092Puttable: ONLY_INVESTORS");
        require(block.timestamp < _bond[bondISIN].maturityDate, "ERC7092Puttable: BOND_MATURED");
        
        uint256 principal =  _principals[msg.sender];
        _principals[msg.sender] = 0;
        
        // ADD LOGIC
    }
}
```

### Convertible Bonds

```solidity
pragma solidity ^0.8.0;
import 'ERC7092.sol';
contract ERC7092Convertible is ERC7092 {
    // WRITE THE LOGIC TO ALLOW INVESTOR OR ISSUER TO CONVERT BONDS TO EQUITY
    // STATE VARIABLES AND FUNCTIONS NEEDED
    
    /**
    * @notice convert bonds to equity. Here we assumed that the investors must convert their bonds to equity
    *         Issuer can also convert invetsors bonds to equity.
    */
    function convert() public {
        require(_principals[msg.sender] > 0, "ERC7092Convertible: ONLY_INVESTORS");
        require(block.timestamp < _bond[bondISIN].maturityDate, "ERC7092Convertible: BOND_MATURED");
        
        uint256 principal =  _principals[msg.sender];
        _principals[msg.sender] = 0;
        
        // ADD LOGIC HERE
    }
}
```

## Security Considerations

Implementing this ERC requires careful consideration of security risks related to functions approving operators to manage owner's bonds and functions allowing bond transfers. Functions `transferAll` and `transferAllFrom` permit transferring the entire balance of an account, necessitating robust validation to ensure only the bond owner or approved accounts can call these functions.

## Copyright

Copyright and related rights waived via [CC0](../LICENSE.md).
