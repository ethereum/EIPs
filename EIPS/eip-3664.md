---
eip: 3664
title: Next generation metaverse NFT
description: This is a standard for next generation metaverse NFTs, which extends ERC-1155.
author: TylerZ (@tylerztl), TY (@nimocat)
discussions-to: https://ethereum-magicians.org/t/eip-3664-next-generation-game-nft-standard/6738
status: Draft
type: Standards Track
category: ERC
created: 2021-07-17
requires: 20, 1155
---

## Abstract
In this proposal, we created the next gen component-NFT standard, which makes up for the problem that [ERC-721](./eip-721.md) and [ERC-1155](./eip-1155.md) can only support unchangeable attributes of NFTs. component-NFT can make the attributes in a NFT have five characteristics: transferable, upgradeable, changeable, combinable and evolvable. By using this standard NFTs in any fields, users can create any valuable dapps based on component-NFT standard.

## Motivation
NFT standards such as ERC721 and ERC1155 are now widely recognized and utilized, but these two protocols lack descriptive power, particularly regarding metadata. As GameFi and Layer 2 solutions emerge and gain popularity, a large of number of avatars, props, etc. are issued as NFT. However, their growth are limited by the nature of existing NFT protocols. It is well known that characters and props typically consist of complex attributes which can change over time. These attributes enrich characters and props, hence changing their value. However, ERC721 and ERC1155 express these attributes via tokenURI, which are typically fixed URL addresses or descriptive texts. Obviously these do not suffice for the mutable attributes required by characters and props.

In our current proposal, we suggest a new type of extendable NFT metadata protocol, which is compatible with existing ERC721 and ERC1155, and can build on their basis to provide better descriptive power for attributes metadata.

Currently we provide the following options for attribute extendability:
1.	General attributes: for describing immutable attributes such as avatar birth dates.
2.	Variable attributes: for describing attributes whose value will change, such as an avatar’s combat power.
3.	Transferable attributes: for describing attributes that can be transferred to other NFTs.
4.	Upgradable attributes: for describing NFT levels and can trigger upgrades.
5.	Evolvable attributes: for describing the fact that NFT can evolve, and evolutions can fail, and if the case of failure, the NFT can no longer be used until properly repaired.
6.	Text attributes: for realizing functionality similar to Loot  since the above attributes are numerical in nature. 


## Specification

The key words "MUST", "MUST NOT", "REQUIRED", "SHALL", "SHALL NOT", "SHOULD", "SHOULD NOT", "RECOMMENDED", "MAY", and "OPTIONAL" in this document are to be interpreted as described in RFC 2119.
Every ERC-3664 compliant contract must implement the ERC3664 and ERC165 interfaces.

```solidity
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

/**
 * @dev Required interface of an ERC3664 compliant contract.
 */
interface ERC3664 {
    /**
     * @dev Emitted when new attribute type `attrId` are minted.
     */
    event AttributeCreated(
        uint256 indexed attrId,
        string name,
        string symbol,
        string uri
    );

    /**
     * @dev Emitted when `value` of attribute type `attrId` are attached to "to"
     * or removed from `from` by `operator`.
     */
    event TransferSingle(
        address indexed operator,
        uint256 from,
        uint256 to,
        uint256 indexed attrId,
        uint256 value
    );

    /**
     * @dev Equivalent to multiple {TransferSingle} events.
     */
    event TransferBatch(
        address indexed operator,
        uint256 from,
        uint256 to,
        uint256[] indexed attrIds,
        uint256[] values
    );

    /**
     * @dev Returns primary attribute type of owned by `tokenId`.
     */
    function primaryAttributeOf(uint256 tokenId)
        external
        view
        returns (uint256);

    /**
     * @dev Returns all attribute types of owned by `tokenId`.
     */
    function attributesOf(uint256 tokenId)
        external
        view
        returns (uint256[] memory);

    /**
     * @dev Returns the attribute type `attrId` value owned by `tokenId`.
     */
    function balanceOf(uint256 tokenId, uint256 attrId)
        external
        view
        returns (uint256);

    /**
     * @dev Returns the batch of attribute type `attrIds` values owned by `tokenId`.
     */
    function balanceOfBatch(uint256 tokenId, uint256[] calldata attrIds)
        external
        view
        returns (uint256[] memory);

    /**
     * @dev Set primary attribute type of owned by `tokenId`.
     */
    function setPrimaryAttribute(uint256 tokenId, uint256 attrId) external;

    /**
     * @dev Attaches `amount` value of attribute type `attrId` to `tokenId`.
     */
    function attach(
        uint256 tokenId,
        uint256 attrId,
        uint256 amount
    ) external;

    /**
     * @dev [Batched] version of {attach}.
     */
    function batchAttach(
        uint256 tokenId,
        uint256[] calldata attrIds,
        uint256[] calldata amounts
    ) external;
}

interface ERC165 {
    /// @notice Query if a contract implements an interface
    /// @param interfaceID The interface identifier, as specified in ERC-165
    /// @dev Interface identification is specified in ERC-165. This function
    ///  uses less than 30,000 gas.
    /// @return `true` if the contract implements `interfaceID` and
    ///  `interfaceID` is not 0xffffffff, `false` otherwise
    function supportsInterface(bytes4 interfaceID) external view returns (bool);
}
```

The first step to apply ERC3664 is to define one or more attributes with _mint or _mintBatch. An NFT creator could attach or batchAttach from a predetermined attribute list to make their NFT. This operation can be completed during the creation of the NFT or may be dynamically applied to update NFT attributes. Hence we have redefined the uniqueness of NFT. It is not just the unique tokenId , but also the list of attributes that differentiate an NFT from others.

## `View` Functions

### `primaryAttributeOf` function

This is an optional method, which can designate a main attribute to an NFT to demonstrate its main purpose. For example, if an NFT is consumable, weaponry or some other type, the main attribute can distinguish different NFTs.

### `attributesOf` function

Obtain the all the attribute IDs of the NFT. Through the attrURI interface one can obtain the attribute list in json template. This json supports display on opensea.

### `balanceOf` function

Obtain the attribute value of a particular NFT attribute.

## Extensions

### Attribute metadata

```solidity
/**
 * @dev Interface for the optional metadata functions from the ERC3664 standard.
 */
interface IERC3664Metadata is IERC3664 {
    /**
     * @dev Returns the name of the attribute.
     */
    function name(uint256 attrId) external view returns (string memory);

    /**
     * @dev Returns the symbol of the attribute.
     */
    function symbol(uint256 attrId) external view returns (string memory);

    /**
     * @dev Returns the Uniform Resource Identifier (URI) for `attrId` attribute.
     */
    function attrURI(uint256 attrId) external view returns (string memory);
}
```

### Text-based Attribute

```solidity
interface IERC3664TextBased is IERC3664Metadata {
    function textOf(uint256 tokenId, uint256 attrId)
        external
        view
        returns (bytes memory);

    function attachWithText(
        uint256 tokenId,
        uint256 attrId,
        uint256 amount,
        bytes memory text
    ) external;

    function batchAttachWithTexts(
        uint256 tokenId,
        uint256[] calldata attrIds,
        uint256[] calldata amounts,
        bytes[] calldata texts
    ) external;
}
```

Using Text-based to extend ERC3664, one can realize Loot like texts NFT projects flexibly.

### Transferable Attribute

```solidity
/**
 * @dev Interface for the transferable functions from the ERC3664.
 */
interface IERC3664Transferable is IERC3664Metadata {
    /**
     * @dev Emitted when  attribute type `attrId` are approved to "to" from `from` by `operator`.
     */
    event AttributeApproval(
        address indexed operator,
        uint256 from,
        uint256 to,
        uint256 attrId
    );

    /**
     * @dev Returns true if `attrId` is approved to token `to` from token `from`.
     */
    function isApproved(
        uint256 from,
        uint256 to,
        uint256 attrId
    ) external view returns (bool);

    /**
     * @dev Approve attribute type `attrId` of token `from` to token `to` called by `from` holder.
     *
     * Emits an {AttributeApproval} event.
     */
    function approve(
        uint256 from,
        uint256 to,
        uint256 attrId
    ) external;

    /**
     * @dev Transfers attribute type `attrId` from token type `from` to `to`.
     *
     * Emits a {TransferSingle} event.
     */
    function transferFrom(
        uint256 from,
        uint256 to,
        uint256 attrId
    ) external;
}
```

Every attribute is an independent component of an NFT. Transferrable attributes can be traded based on its protocol. After the NFT beneficiary receives the new attribute, the original NFT ID remains unchanged, but its price and rarity will increase.

### Updatable Attribite

```solidity
/**
 * @dev Interface for the updatable functions from the ERC3664.
 */
interface IERC3664Updatable is IERC3664Metadata {
    /**
     * @dev Remove attribute type `attrId` from `tokenId`.
     */
    function remove(uint256 tokenId, uint256 attrId) external;

    /**
     * @dev Increases `amount` value of attribute type `attrId` to `tokenId`.
     */
    function increase(
        uint256 tokenId,
        uint256 attrId,
        uint256 amount
    ) external;

    /**
     * @dev Decreases `amount` value of attribute type `attrId` from `tokenId`.
     */
    function decrease(
        uint256 tokenId,
        uint256 attrId,
        uint256 amount
    ) external;
}
```

Updatable attributes of NFTs can change under certain trigger conditions. Such use cases are common in gaming and finance. For example, the life force of an character, combative power of weaponry, debt interest rates, etc.

### Upgradable Attribute

```solidity
interface IERC3664Upgradable is IERC3664Metadata {
    event AttributeUpgraded(
        uint256 indexed tokenId,
        uint256 indexed attrId,
        uint8 level
    );

    function levelOf(uint256 tokenId, uint256 attrId)
        external
        view
        returns (uint8);

    function mintWithLevel(
        uint256 attrId,
        string memory name,
        string memory symbol,
        string memory uri,
        uint8 maxLevel
    ) external;

    function upgrade(
        uint256 tokenId,
        uint256 attrId,
        uint8 level
    ) external;
}
```

This attribute upgrades NFTs with levels. NFT creators can set the maximum level of the NFT, and provide upgrade methodology for users. When the NFT satisfies upgrade conditions again, it cannot be upgraded more than once. 


### Evolvable Attribute

```solidity
/**
 * @dev Interface for the Evolvable functions from the ERC3664.
 */
interface IERC3664Evolvable is IERC3664Metadata {
    event AttributeEvolvable(
        address indexed operator,
        uint256 tokenId,
        uint256 attrId,
        bool status
    );

    event AttributeRepaired(
        address indexed operator,
        uint256 tokenId,
        uint256 attrId
    );

    function period(uint256 tokenId, uint256 attrId)
        external
        view
        returns (uint256);

    function evolutive(uint256 tokenId, uint256 attrId) external;

    function repair(uint256 tokenId, uint256 attrId) external;
}
```

Evolvable attributes describe how NFTs cans strengthen attributes via evolution, which yields uncertain success with different probabilities. NFT creators can set the probability of success of each evolution. NFTs that fail to evolve become useless. They can only be used and evolved normally after proper repairs. This feature provides templates for evolutionary functions for the metaverse NFTs, which can be modified as needed for other projects that require evolutionary approaches.

## Rational
ERC3664 itself does not create any NFTs, and is only used for storing and expressing NFT attributes. Therefore it is a completely new NFT attribute extendability standard that seeks to tokenize attributes. Every attribute has its own independent ID, and can be independently transferred and combined to create richly diverse digital artworks with NFTs, and not just fixed collectibles.

## Compatibility
ERC3664 is compatible to any kind of NFT protocol of any standard. It functions merely as an NFT supplementary protocol rather than as a replacement protocol. Hence most NFT works in the markets do not need to be modified and need to realize ERC3664 by appending attributes the existing NFT token. The original work can be displayed through ERC3664’s tokenURI. Support for ERC721 and ERC1155 is non-intrusive.

## Use Cases
[Metacore](https://github.com/DRepublic-io/drepublic-contracts/blob/main/contracts/Metacore.sol)

## Reference Implementation
https://github.com/DRepublic-io/drepublic-contracts

## Security Considerations
Since the owner becomes vacuous after every ERC-721 is aggregated by ERC3664, and since each contract only allows the contract owner to invoke functions in contract，ERC-721 and ERC-1155 aggregated by EIP-3664 are guaranteed to be safe.

## Copyright
Copyright and related rights waived via [CC0](https://creativecommons.org/publicdomain/zero/1.0/).
